/**
 * =====================================
 * #GetScriptResult ( name )
 *		https://github.com/petrowsky/fmpstandards/blob/master/Functions/%23GetScriptResult.fmfn
 *
 * PURPOSE:
 *		Retrieves values from a name-value pair list created with the # ( name ;
 *		value ) function in the script result. This function does not set any
 *		variables, making it non-destructive, in contrast with the
 *		#AssignScriptResults function, which can over-write variables. This
 *		function is equivalent to #Get ( Get ( ScriptResult ) ; name ).
 *
 * RETURNS:
 *		The value from the name-value pair specified by the name parameter. If
 *		such a value is not in the script result, the function returns Null.
 *
 * PARAMETERS:
 *		name: The name of the name-value pair from which to retrieve the value.
 *
 * DEPENDENCIES: none
 *
 * RELEASE:
 *		2011-02-13
 *
 * REFERENCES:
 *		Script Parameter Interface Best Practice: http://filemakerstandards.org/pages/viewpage.action?pageId=557462
 * =====================================
 */

Let ( [
	~name = "¶$" & name & " = ";
	~parameters = Get ( ScriptResult );
	~namePosition =
		Position (
			"¶" & ~parameters;
			~name;
			Length ( ~parameters ) + 1;
			-1
		);
	~valuePosition = ~namePosition + Length ( ~name ) - 1;
	~endPosition = Position ( ~parameters ; ";¶" ; ~namePosition ; 1 );
	~value =
		Middle (
			~parameters;
			~valuePosition;
			~endPosition - ~valuePosition
		);
	~error = not ~namePosition or EvaluationError ( ~value )
];
	If ( ~error ; "" ; /*else*/ Evaluate ( ~value ) )
)