/*
 * =====================================================
 * WindowProperties ( method )
 * 
 * PARAMETERS:
 *                @method (enum) default, piped, let, variables
 * RETURNS:
 *                (list) window properties according to specified method
 * DEPENDENCIES:
 *                ObjectID(),VariableList()
 * NOTES:
 *                Provides multiple methods for returning the
 *                properties of the current window
 *                See the WindowCenter companion function
 * =====================================================
 * 
 */

Let ( [
	var.keys = List ( "name"; "layout"; "id"; "top";"left";"width";"height");
	var.parent = List (
		"parentWindowName";
		"parentWindowLayout";
		"parentWindowID";
		"parentWindowTop";
		"parentWindowLeft";
		"parentWindowWidth";
		"parentWindowHeight";
		);
	var.values = List (
						Get ( WindowName );
						Get ( LayoutName );
						ObjectID ( Get ( LayoutName ); "Layout"; ""; "" );
						Get ( WindowTop );
						Get ( WindowLeft );
						Get ( WindowWidth );
						Get ( WindowHeight )
	)
];
	// returns
	Case(
		method = "" or
		method = "default";
			var.values;

		method = "piped";
			Substitute ( var.values ; Â¶ ; "|" );

		// global variables are returned by VariableList() - use Substitute ( properties ; "$$" ; "$" ) to make local
		method = "let";
			VariableList ( var.keys; var.values ; "global" );
			
		method = "parentVars";
			VariableList ( var.parent; var.values ; "local" );

		// default
		var.values
	)
)